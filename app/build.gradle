apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: 'dependencies.gradle'
apply from: 'flavors.gradle'

def credentialsPropertiesFile = rootProject.file("credentials.properties")
def credentialsProperties = new Properties()
credentialsProperties.load(new FileReader(credentialsPropertiesFile))

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        buildConfigField "Long", "CONNECTION_TIMEOUT", "30L"
        buildConfigField "Long", "READ_TIMEOUT", "30L"
        buildConfigField "Long", "WRITE_TIMEOUT", "30L"
        buildConfigField "String", "API_URL", "\"https://api.nasa.gov/neo/rest/v1/\""
        buildConfigField "String", "API_KEY", credentialsProperties['apiKey']
        buildConfigField "String", "API_DATE_FORMAT", "\"yyyy-MM-dd\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    flavorDimensions "APP"

    configurations.all {
        resolutionStrategy.eachDependency {
            DependencyResolveDetails details ->
                def requested = details.requested
                if (requested.group == 'com.android.support') {
                    if (!requested.name.startsWith("multidex")) {
                        details.useVersion("$appCompatVersion")
                    }
                }
        }
    }
}